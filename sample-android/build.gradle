import com.chrynan.colors.buildSrc.LibraryConstants

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "org.jetbrains.compose"

group LibraryConstants.group
version LibraryConstants.versionName

android {
    compileSdkVersion LibraryConstants.Android.compileSdkVersion

    defaultConfig {
        applicationId "com.chrynan.colors.sample.android"
        minSdkVersion 26
        targetSdkVersion LibraryConstants.Android.targetSdkVersion
        versionCode LibraryConstants.versionCode
        versionName LibraryConstants.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        // Opt-in to experimental compose APIs
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xallow-jvm-ir-dependencies"
    }
}

tasks.withType(org.gradle.jvm.tasks.Jar).all { duplicatesStrategy = DuplicatesStrategy.INCLUDE }

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    cleanedAnnotations
    compile.exclude group: "org.jetbrains", module: "annotations"
    "implementation" {
        exclude group: "androidx.compose.material"
        exclude group: "androidx.compose.runtime"
        exclude group: "androidx.compose.ui"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "androidx.appcompat:appcompat:1.3.0"

    implementation(compose.runtime)
    implementation(compose.ui)
    implementation(compose.material)

    implementation "androidx.compose.compiler:compiler:1.0.0-beta09"
    implementation "androidx.compose.ui:ui-tooling:1.0.0-beta09"
    implementation "androidx.activity:activity-compose:1.3.0-beta02"

    implementation project(":colors-jetpack-compose")
}
